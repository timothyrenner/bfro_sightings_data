LINE bigfoot
    TITLE "Bigfoot Sightings by Year"
    X date BY YEAR LABEL "Year of Sighting"
    Y number COUNT LABEL "Number of Sightings"
    SPLIT BY classification
    -- The filter string gets pasted into a SQL WHERE clause.
    FILTER "date > '1960-01-01'"

BAR bigfoot
    TITLE "Bigfoot Sightings by State"
    X state LABEL "State"
    Y state COUNT LABEL "Number of Sightings" SORT DESC

CONCAT(
    (
        HISTOGRAM bigfoot
            TITLE "Bigfoot Sighting Moon Phases"
            X moon_phase LABEL "Moon Phase"
            STEP 0.1
        
        BAR bigfoot
            TITLE "Bigfoot Sightings by Season"
            X season LABEL "Season"
            Y number COUNT LABEL "Number of Sightings"
    )

    (
        BAR bigfoot
            TITLE "Number of Bigfoot Sightings by Classification"
            X classification LABEL "Sighting Classification"
            Y number COUNT LABEL "Number of Sightings"

        PIE bigfoot
            TITLE "Number of Geocoded Sightings"
            AXIS TRANSFORM 
                "CASE WHEN latitude IS NULL THEN 'No Location' 
                 ELSE 'Location' END"
            HOLE 0.3
    )
)

SCATTER bigfoot
    TITLE "Bigfoot Sighting Temperature by Latitude"
    X latitude LABEL "Latitude"
    Y temperature_mid LABEL "Temperature (F)"
    COLOR BY moon_phase "YlOrRd" LABEL "Moon Phase"